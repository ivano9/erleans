%%% ---------------------------------------------------------------------------
%%% @author Tristan Sloughter <tristan.sloughter@spacetimeinsight.com>
%%% @copyright 2016 Space-Time Insight <tristan.sloughter@spacetimeinsight.com>
%%%
%%% @doc
%%% @end
%%% ---------------------------------------------------------------------------
-module(dist_lifecycle_SUITE).

-compile(export_all).

-include_lib("eunit/include/eunit.hrl").
-include_lib("common_test/include/ct.hrl").

-include("test_utils.hrl").

-define(NODE_CT, 'ct@127.0.0.1').
-define(NODE_A, 'a@127.0.0.1').

all() ->
    [
        manual_start_stop,
        activate_callback,
        deduplication
    ].

init_per_suite(Config) ->
    application:load(partisan),
    application:load(erleans),
    application:set_env(partisan, peer_port, 10200),
    application:set_env(partisan, pid_encoding, false),
    application:set_env(
        partisan, partisan_peer_service_manager, partisan_pluggable_peer_service_manager
    ),
    %% lower gossip interval of partisan membership so it triggers more often
    %% in tests
    application:set_env(partisan, periodic_enabled, true),
    application:set_env(partisan, periodic_interval, 100),
    logger:set_application_level(partisan, error),
    logger:set_application_level(erleans, debug),
    {ok, _} = application:ensure_all_started(partisan),
    {ok, _} = application:ensure_all_started(bondy_mst),
    {ok, _} = application:ensure_all_started(erleans),
    start_nodes(),
    Config.

end_per_suite(_Config) ->
    application:stop(erleans),
    application:stop(partisan),
    application:stop(bondy_mst),
    application:unload(erleans),
    application:unload(bondy_mst),

    {ok, _} = erpc:call(?NODE_A, init, stop, []),

    ok.

%% =============================================================================
%% TEST CASES
%% =============================================================================

manual_start_stop(_Config) ->
    ok = join_nodes(),
    Grain1 = erleans:get_grain(test_grain, <<"grain1">>),
    Grain2 = erleans:get_grain(test_grain, <<"grain2">>),

    ?assertEqual(
        {ok, 1},
        test_grain:activated_counter(Grain1)
    ),

    ?assertEqual(
        {ok, 1},
        rpc:call(?NODE_A, test_grain, activated_counter, [Grain2])
    ),

    %% ensure we've waited a broadcast interval
    timer:sleep(500),

    ProcRef1 = erleans_pm:whereis_name(Grain1),
    ProcRef2 = erleans_pm:whereis_name(Grain2),

    ct:pal("Grain1: ~p, At: ~p", [Grain1, ProcRef1]),
    ct:pal("Grain2: ~p, At: ~p", [Grain2, ProcRef2]),

    %% verify grain1 is on node ct and grain2 is on node a
    ?assertEqual(?NODE_CT, partisan_remote_ref:node(ProcRef1)),
    ?assertEqual(?NODE_A, partisan_remote_ref:node(ProcRef2)),

    ?assertEqual({ok, ?NODE_CT}, rpc:call(?NODE_A, test_grain, node, [Grain1])),
    ?assertEqual({ok, 1}, rpc:call(?NODE_A, test_grain, activated_counter, [Grain2])),

    timer:sleep(200),

    ?assertEqual({ok, ?NODE_A}, rpc:call(?NODE_A, test_grain, node, [Grain2])),
    ?assertEqual({ok, ?NODE_A}, test_grain:node(Grain2)),

    ok.

activate_callback(_Config) ->
    ok = join_nodes(),
    meck:new(test_grain, [passthrough]),
    meck:expect(
        test_grain,
        placement,
        fun() -> {callback, ?MODULE, activate_callback_placement} end
    ),
    Grain3 = erleans:get_grain(test_grain, <<"grain3">>),
    Expected = activate_callback_placement(Grain3),
    ?assertEqual({ok, Expected}, test_grain:node(Grain3)).

deduplication(_Config) ->
    GrainRef = erleans:get_grain(test_grain, <<"grain1">>),

    %% We create duplicate
    ?assertEqual(
        {ok, 1},
        test_grain:activated_counter(GrainRef)
    ),
    ?assertEqual(
        {ok, 1},
        rpc:call(?NODE_A, test_grain, activated_counter, [GrainRef])
    ),

    [_LocalProcRef] = erleans_pm:lookup(GrainRef),
    [RemoteProcRef] = rpc:call(?NODE_A, erleans_pm, lookup, [GrainRef]),

    %% We override NODE_CT's functions so that we define RemoteProcRef to be
    %% in the right location but not LocalProcRef. This should depuplicate,
    %% forcing LocalProcRef to be deactivated, during AAE sync.
    meck:new(erleans_pm, [passthrough]),
    meck:new(erleans_grain, [passthrough]),

    meck:expect(
        erleans_pm,
        is_reachable,
        fun
            (P) when P == RemoteProcRef ->
                true;
            (P) ->
                meck:passthrough(P)
        end
    ),

    meck:expect(
        erleans_grain,
        is_location_right,
        fun
            (G, P) when G == GrainRef andalso P == RemoteProcRef ->
                true;
            (_, _) ->
                %% The local ref
                false
        end
    ),

    %% We join the cluster and trigger an AAE sync
    ok = join_nodes(),
    ok = erleans_pm:exchange(?NODE_A),

    timer:sleep(3000),

    %% LocalProcRef should be gone
    ?assertEqual(
        [RemoteProcRef],
        erleans_pm:lookup(GrainRef)
    ),
    ?assertEqual(
        [RemoteProcRef],
        rpc:call(?NODE_A, erleans_pm, lookup, [GrainRef])
    ),

    meck:unload(erleans_pm),
    meck:unload(erleans_grain),

    ok.

%% =============================================================================
%% PRIVATE
%% =============================================================================

start_nodes() ->
    %, b, c, d],
    Nodes = [{?NODE_A, 10201}],
    ct:log("Starting nodes ~p", [Nodes]),
    start_nodes(Nodes, []).

start_nodes([], Acc) ->
    Acc;
%% start_nodes([{Node, PeerPort} | T], Acc) ->
%%     ct:log("Starting node ~p", [Node]),
%%     CodePath = code:get_path(),
%%     Paths = lists:flatten([["-pa ", Path, " "] || Path <- CodePath]),
%%     ErlFlags = "-config ../../../../test/sys.config " ++ Paths,

%%     {ok, HostNode} = ?CT_PEER(#{name => Node},[
%%         {kill_if_fail, true},
%%         {monitor_master, true},
%%         {init_timeout, 3000},
%%         {startup_timeout, 3000},
%%         {startup_functions, [
%%             {logger, set_handler_config, [default, config,
%%                 #{file => "log/ct_console.log"}
%%             ]},
%%             {logger, set_handler_config, [default, formatter,
%%              {logger_formatter, #{}}]},
%%             {application, load, [partisan]},
%%             {application, load, [bondy_mst]},
%%             {application, load, [erleans]},
%%             {application, set_env, [partisan, pid_encoding, false]},
%%             {application, set_env, [partisan, remote_ref_as_uri, true]},
%%             {application, set_env, [partisan, periodic_enabled, true]},
%%             {application, set_env, [partisan, periodic_interval, 100]},
%%             {application, set_env, [partisan, peer_port, PeerPort]},
%%             {application, ensure_all_started, [partisan]},
%%             {application, ensure_all_started, [bondy_mst]},
%%             {application, ensure_all_started, [erleans]}
%%         ]},
%%         {erl_flags, ErlFlags}
%%     ]),
%%     timer:sleep(1000),

%%     ct:pal("Node ~p [OK]", [HostNode]),
%%     true = net_kernel:connect_node(?NODE_A),
%%     start_nodes(T, [HostNode | Acc]).

start_nodes([{Node, PeerPort} | T], Acc) ->
    CodePath = code:get_path(),
    Paths = "-pa " ++ lists:flatten([[Path, " "] || Path <- CodePath]),
    StartupFuns = [
        {logger, set_handler_config, [
            default,
            config,
            #{file => "log/ct_console.log"}
        ]},
        {logger, set_handler_config, [
            default,
            formatter,
            {logger_formatter, #{}}
        ]},
        {application, load, [partisan]},
        {application, load, [bondy_mst]},
        {application, load, [erleans]},
        {application, set_env, [partisan, pid_encoding, false]},
        {application, set_env, [partisan, periodic_enabled, true]},
        {application, set_env, [partisan, periodic_interval, 100]},
        {application, set_env, [partisan, peer_port, PeerPort]},
        {application, set_env, [
            partisan, partisan_peer_service_manager, partisan_pluggable_peer_service_manager
        ]},
        {application, ensure_all_started, [partisan]},
        {application, ensure_all_started, [bondy_mst]},
        {application, ensure_all_started, [erleans]}
    ],

    ct:log("Starting node ~p from node ~p paths ~p", [Node, node(), Paths]),

    {ok, Pid, HostNode} = peer:start_link(#{
        name => Node,
        %% longnames => true,
        args => [
            "-config ../../../../test/sys.config",
            Paths
        ]
    }),

    ct:pal("Node ~p [OK]", [HostNode]),

    _ = [peer:call(Pid, M, F, A) || {M, F, A} <- StartupFuns],

    timer:sleep(1000),
    true = net_kernel:connect_node(Node),
    start_nodes(T, [HostNode | Acc]).

join_nodes() ->
    rpc:call(?NODE_A, partisan_peer_service, join, [
        #{
            name => ?NODE_CT,
            listen_addrs => [#{ip => {127, 0, 0, 1}, port => 10200}],
            parallelism => 1
        }
    ]),

    ok = partisan_peer_service:join(#{
        name => ?NODE_A,
        listen_addrs => [#{ip => {127, 0, 0, 1}, port => 10201}],
        parallelism => 1
    }),

    ok.

%% used by activate_callback
activate_callback_placement(_GrainRef) ->
    ?NODE_A.
